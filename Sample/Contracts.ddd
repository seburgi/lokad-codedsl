// Select a specific generator, default is TemplatedGenerator
class TemplatedGenerator
// main namespace for commands and messages
namespace Lokad
// define data contract namespace for all classes
extern "Lokad"

// you can define custom string templates for your generator
ClassNameTemplate = @"
    [DataContract(Namespace = {1})]
    public partial class {0}"

// define shortcut for interface IIdentity
if ! = IIdentity

// Simple value object
UniverseId!(long id)
GalaxyId!(long id)

Universe(UniverseId Id, string name)

// define alias
const name = string name
const dateUtc = DateTime dateUtc

// Define application service IUniverseApplicationService
interface Universe(UniverseId Id)
{
    // define shortcut for commands
    if ? = IUniverseCommand
    // define shortcut for events
    if ! = IUniverseEvent<UniverseId>

    CreateUniverse?(name)
        // override ToString() for command
        explicit "Create universe - {name}"
        UniverseCreated!(name)
            // override ToString() for event
            explicit "Universe {name} created"

    DestroyUniverse?(string reason)
        explicit "Destroy universe, reason - {reason}"
        UniverseDestroyed!(string reason)
            explicit "Universe destroyed, reason - {reason}"

    AddGalaxy?(name, GalaxyType type)
        explicit "Create {type} galaxy - {name}"
        GalaxyAdded!(GalaxyId galaxyId, name, GalaxyType type, dateUtc)
            explicit "[{dateUtc:yyyy.MM.dd HH:mm}] Galaxy {name} of type {type} added to universe."
    WipeGalaxy?(GalaxyId galaxyId, string reason)
        explicit "Wipe galaxy {galaxyId}, reason - {reason}"
        GalaxyWiped!(GalaxyId galaxyId, name, string reason, dateUtc)
            explicit "[{dateUtc:yyyy.MM.dd HH:mm}] Galaxy {name} wiped, reason - {reason}"
}